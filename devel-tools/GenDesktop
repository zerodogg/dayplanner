#!/usr/bin/perl
# GenDesktop
# Generates the .desktop file for Day Planner
# $Id$
# Copyright (C) Eskild Hustvedt 2006
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

use strict;			# Force strict coding
use warnings;			# Tell perl to warn about things
use Locale::gettext;		# Allow the program to be translated
use FindBin;			# So that we can detect stuff at runtime
use POSIX;
use utf8;
use Fatal qw/open close/;

sub SetLocale {
	foreach(keys(%ENV)) {
		next unless /^(LC|LANG)/;
		$ENV{$_} = $_[0];
	}
	$ENV{LC_ALL} = $_[0];
	setlocale(LC_ALL, "" );
	textdomain("dayplanner");
	bindtextdomain("dayplanner", "./");
	my $GettextObj = Locale::gettext->domain_raw('dayplanner');	# Set the gettext domain
	$GettextObj->codeset('UTF-8');
	return($GettextObj);
}

my %Languages;

my($BinDir, $IconsDir) = @ARGV;
my $Icon;

unless(defined($BinDir)) {
	die("Usage: $0 [DIRECTORY CONTAINING THE dayplanner BINARY] [ICONS DIR]\n The binary dir is mandatory, icons dir optional.");
}

if(defined($IconsDir)) {
	$Icon = "$IconsDir/dayplanner-48x48.png";
} else {
	$Icon = "dayplanner.png";
}

# Set dirs
chdir($FindBin::RealBin);
chdir("..");

print "Running ./devel-tools/BuildLocale\n";
system("./devel-tools/BuildLocale");

print "Generating .desktop...:\n";
chdir("locale");
foreach(<*>) {
	if(-d $_ and not -l $_) {
		my $Gettext = SetLocale($_);
		$Languages{$_} = $Gettext->get("Day Planner");
		if($Languages{$_} eq "Day Planner") {
			delete($Languages{$_});
			print "$_: No valid translation of \"Day Planner\" found\n";
		} else {
			print "$_: OK\n";
		}
	}
}
# Create the file
chdir("..");
print "Writing...";
open(my $Desktop, ">", "./doc/dayplanner.desktop");
print ".";
binmode($Desktop, ':utf8');
print $Desktop "[Desktop Entry]\n";
print $Desktop "Version=1.0\n";
print $Desktop "Encoding=UTF-8\n";
print $Desktop "MimeType=text/calendar\n";
print $Desktop "Categories=X-MandrivaLinux-Office-TimeManagement;Office;Calendar;GTK;GNOME;\n";
if($BinDir eq '.') {
	print $Desktop "Exec=$BinDir/dayplanner\n";
} else {
	print $Desktop "Exec=dayplanner\n";
}
print $Desktop "Icon=$Icon\n";
print $Desktop "Name=Day Planner\n";
print $Desktop "StartupNotify=false\n";
print $Desktop "Terminal=false\n";
print $Desktop "Type=Application\n";
print ".";
print $Desktop "Name[$_]=$Languages{$_}\n" foreach(keys(%Languages));
print ".";
close($Desktop);
print "done\n";
print "Wrote ./doc/dayplanner.desktop\n";
