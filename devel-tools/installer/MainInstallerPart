#!/usr/bin/perl
# MainInstallerPart
# Copyright (C) Eskild Hustvedt 2006
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.

# This is a quick "hack" of an installer for Day Planner

use strict;
use warnings;
use Gtk2;
use File::Copy;
use File::Path;
use POSIX ":sys_wait_h";

my $ProgressWin;
my $InstallDirectory = "$ENV{HOME}/.local/share/dayplanner";

my $AutoInstaller;
my $TotalSteps = 12;

# Purpose: Create a progresswindow
# Usage: my $ProgressWin = DPCreateProgressWin(WINDOW NAME, INITIAL PROGRESS BAR TEXT, PULSATE_MODE?);
# 	Returns a hashref with the following keys:
# 		Window = The window
# 		ProgressBar = The progress bar
sub DPCreateProgressWin {
	my ($Name, $Text, $PulsateMode) = @_;
	my %ProgressHash;
	$ProgressHash{Window} = Gtk2::Window->new();
	$ProgressHash{Window}->set_skip_taskbar_hint(1);
	$ProgressHash{Window}->set_skip_pager_hint(1);
	if(defined($Name)) {
		$ProgressHash{Window}->set_title($Name);
	}
	$ProgressHash{ProgressBar} = Gtk2::ProgressBar->new();
	$ProgressHash{Window}->add($ProgressHash{ProgressBar});
	$ProgressHash{Window}->set_modal(1);
	$ProgressHash{Window}->set_resizable(0);
	if(defined($Text)) {
		$ProgressHash{ProgressBar}->set_text($Text);
	} else {
		$ProgressHash{ProgressBar}->set_fraction(0);
	}
	if($PulsateMode) {
		$ProgressHash{ProgressBar}->{activity_mode} = 0;
	}
	$ProgressHash{ProgressBar}->show();
	$ProgressHash{Window}->show();
	Gtk2->main_iteration while Gtk2->events_pending;
	Gtk2->main_iteration while Gtk2->events_pending;
	Gtk2->main_iteration while Gtk2->events_pending;
	return(\%ProgressHash);
}

# Purpose: Set the progress of a progressbar
# Usage: ProgressMade(FUNCTIONS TO PERFORM, FUNCTIONS PERFORMED, $ProgressWindowHashref, NewText?, No_Dot?);
sub ProgressMade {
	my ($ToPerform, $Performed, $ProgressHash,$Text,$NoDot) = @_;
	print "." unless(defined($NoDot));
	return unless defined($ProgressHash);
	my $Bar = $ProgressHash->{ProgressBar};
	my $Result = sprintf("%d", ($Performed / $ToPerform) * 100);
	if($Result < 100) {
		$Result = "0.$Result"; 
	} else {
		$Result = 1;
	}
	$Bar->set_fraction($Result);
	if($Text) {
		$Bar->set_text($Text);
	}
	Gtk2->main_iteration while Gtk2->events_pending;
	Gtk2->main_iteration while Gtk2->events_pending;
	Gtk2->main_iteration while Gtk2->events_pending;
	Gtk2->main_iteration_do(0);
	Gtk2->main_iteration_do(0);
	Gtk2->main_iteration_do(0);
	return(1, $Result);
}

# Purpose: Run a system() in a child
# Usage: SystemChild(OPTIONS);
#  OPTIONS are identical to system();
sub SystemChild {
	my $PID = fork;
	my $Return;
	if($PID) {
		if($AutoInstaller) {
			while(not waitpid($PID, WNOHANG)) {
				Gtk2->main_iteration while Gtk2->events_pending;
				Gtk2->main_iteration_do(0);
			}
		} else {
			waitpid($PID,0);
		}
		$Return = $?;
	} else {
		exec(@_);
	}
	return($Return);
}

$| = 1;

print "Perl installer part...\n";
print "Using install dir: $InstallDirectory\n";
my $Internal_Version = '$Id$';
print "Installer version: $Internal_Version\n";

die("Fatal internal error: Perl installer part got no data directory supplied\n") unless(defined($ARGV[0]));
die("Fatal internal error: The data directory supplied ($ARGV[0]) does not exist.\nThis probably means that this package is corrupt, try re-downloading it.\n") unless -e $ARGV[0];

my $InstallerData = $ARGV[0];
print "Installer data is in $InstallerData\n";

sub InPath {
	foreach (split /:/, $ENV{PATH}) {
		if (-x "$_/@_") {
			return 1;
		}
	}
	return 0;
}


if(defined($ARGV[1]) and $ARGV[1] eq 'auto') {
	print "Using automatic install - skipping prompts\n";
	$AutoInstaller = 1;
} else {
	Gtk2->init();
	my $Dialog = Gtk2::MessageDialog->new(undef, "modal", 'question', 'yes-no', "Welcome to the day planner installation program. This will install day planner into a subdirectory of your home directory and put a day planner icon on your desktop. All of this will be done automatically.\n\nDo you want to continue?");
	my $Reply = $Dialog->run();
	$Dialog->destroy();
	unless($Reply eq 'yes') {
		print "Exiting as requested by user\n";
		exit(0);
	}
	# Flush the display
	Gtk2->main_iteration while Gtk2->events_pending;

	$ProgressWin = DPCreateProgressWin("Installing", "Installing Day Planner...", 0);
}
if(-d $InstallDirectory) {
	print "Found old install, running uninstaller first...\n";
	Gtk2->main_iteration_do(0) foreach(1..15);;
	if(-e "$InstallDirectory/uninstall.sh") {
		SystemChild("sh '$InstallDirectory/uninstall.sh'") and print "Uninstaller failed, expect trouble.\n";
	} else {
		print "Failed to run uninstaller, expect trouble.\n";
	}
}
ProgressMade($TotalSteps, 1, $ProgressWin,undef,1);

print "Installing...";
SystemChild("mkdir -p $InstallDirectory");

if(defined($ENV{DAYPLANNER_CP_SILENTERRORS}) and $ENV{DAYPLANNER_CP_SILENTERRORS} eq 'TRUE') {
	# When using the local installer we might get svn files which will make cp display
	# harmless errors. Setting DAYPLANNER_CP_SILENTERRORS to TRUE silences cp
	SystemChild("cp -r $InstallerData/* $InstallDirectory/ &>/dev/null");
} else {
	SystemChild("cp -r $InstallerData/* $InstallDirectory/");
}

ProgressMade($TotalSteps, 2, $ProgressWin);

my @BINDIRS = (
	"$ENV{HOME}/bin",
	"$ENV{HOME}/.bin",
	"$ENV{HOME}/.local/bin",
	"/usr/local/bin",
);

ProgressMade($TotalSteps, 3, $ProgressWin);

my @BINLINKS;

my $BinDirCount;
foreach(@BINDIRS) {
	$BinDirCount++;
	if(-e $_ and -d $_ and -w $_) {
		symlink("$InstallDirectory/dayplanner", "$_/dayplanner");
		symlink("$InstallDirectory/dayplanner-daemon", "$_/dayplanner-daemon");
		symlink("$InstallDirectory/dayplanner-notifier", "$_/dayplanner-notifier");
		push(@BINLINKS, $_)
	}
	ProgressMade($TotalSteps, $TotalSteps + 2, $ProgressWin);
}
# Write uninstall script
open(UNINSTALL, ">$InstallDirectory/uninstall.sh");
print UNINSTALL "#!/bin/sh\n";
print UNINSTALL "echo -n \"Uninstalling...\"\n";
print UNINSTALL "cd \$HOME\n";
print UNINSTALL "rm -rf $InstallDirectory\n";
print UNINSTALL "echo -n \".\"\n";
foreach(@BINLINKS) {
	print UNINSTALL "rm -f $_/dayplanner $_/dayplanner-daemon $_/dayplanner-notifier\n";
	print UNINSTALL "echo -n \".\"\n";
}
print UNINSTALL "[ -e \$HOME/.menu/dayplanner.menu ] && rm -f \$HOME/.menu/dayplanner.menu && update-menus &> /dev/null\n";
print UNINSTALL "echo -n \".\"\n";
print UNINSTALL "[ -e \$HOME/Desktop/dayplanner.desktop ] && rm -f \$HOME/Desktop/dayplanner.desktop\n";
print UNINSTALL "echo -n \".\"\n";
print UNINSTALL '[ -e "$HOME/.local/share/applications/dayplanner.desktop" ] && rm -f  "$HOME/.local/share/applications/dayplanner.desktop"' . "\n";
print UNINSTALL "echo \"done\"\n";
print UNINSTALL "echo \"Day planner has been uninstalled.\"\n";
close(UNINSTALL);
SystemChild("chmod +x $InstallDirectory/uninstall.sh");

ProgressMade($TotalSteps, 8, $ProgressWin);

# Write the XDG-style menu item
if(-e "$InstallDirectory/doc/dayplanner.desktop") {
	SystemChild("perl","-pi","-e","s#DAYPLANNER_INST_DIR#$InstallDirectory#g","$InstallDirectory/doc/dayplanner.desktop");
	ProgressMade($TotalSteps, 9, $ProgressWin);
	mkpath("$ENV{HOME}/.local/share/applications/");
	copy("$InstallDirectory/doc/dayplanner.desktop", "$ENV{HOME}/.local/share/applications/dayplanner.desktop");
	ProgressMade($TotalSteps, 10, $ProgressWin);
	if(-d "$ENV{HOME}/Desktop/") {
		copy("$InstallDirectory/doc/dayplanner.desktop", "$ENV{HOME}/Desktop/");
	}
}

ProgressMade($TotalSteps, 11, $ProgressWin);
# Write Debian-style menu item
if(InPath("update-menus")) {
	mkdir("$ENV{HOME}/.menu/") unless -d "$ENV{HOME}/.menu/";
	open(MENU, ">$ENV{HOME}/.menu/dayplanner.menu");
	print MENU "?package(%{name}):command=\"$InstallDirectory/dayplanner\"  icon=\"$InstallDirectory/art/dayplanner_48.png\"         needs=\"x11\"         section=\"Office/Time Management\"         title=\"Day planner\"         longtitle=\"An easy to use graphical day planner\"\n";
	close(MENU);
	# update-menus doesn't block, so it does more harm than good to use
	# SystemChild().
	system("update-menus &>/dev/null");
}
ProgressMade($TotalSteps, 12, $ProgressWin);

print "done\n";
unless(defined($ARGV[1]) and $ARGV[1] eq 'auto') {
	$ProgressWin->{Window}->destroy();
	my $Finished = Gtk2::MessageDialog->new(undef, "modal", 'info', 'ok', "You can start day planner by clicking on the icon on your desktop.\n\n(If it doesn't work, you can run $InstallDirectory/dayplanner directly)\n\nIf you at some point want to uninstall day planner run $InstallDirectory/uninstall.sh");
	$Finished->run();
	$Finished->destroy();
}
print "Installer successfully finished!\n";
exit(0);
